--
-- Copyright (c) 2022, Digital Asset (Switzerland) GmbH and/or its affiliates. All rights reserved.
-- SPDX-License-Identifier: Apache-2.0
--

{-# LANGUAGE AllowAmbiguousTypes #-}

module Trigger.AutoApprove where

import DA.Action (void, when)
import DA.Foldable (forA_)
import DA.List.Total (head)
import DA.Optional (optionalToList)
import Daml.Trigger
import Model.Blacklist
import Model.Balance
import Workflow.TransferProposal
import Workflow.Data
import DA.Map qualified as Map

autoApproveTrigger : Trigger ()
autoApproveTrigger = Trigger
  { initialize = pure ()
  , updateState = \_ -> pure ()
  , rule = autoApprove
  , registeredTemplates = RegisteredTemplates [
      registeredTemplate @AutoApproveTransferProposalMarker,
      registeredTemplate @TransferProposal,
      registeredTemplate @Blacklist,
      registeredTemplate @Balance
    ]
  , heartbeat = None
  }

autoApprove : Party -> TriggerA () ()
autoApprove sender = do
  blackLists <- query @Blacklist

  let blacklist = snd <$> head blackLists

  transferProposals <- query @TransferProposal
  autoApproveMarkersWithCids <- query @AutoApproveTransferProposalMarker
  let markers = Map.fromList $ (\(_, marker) -> (marker.address, marker)) <$> autoApproveMarkersWithCids

  forA_ transferProposals \(cid, transferProposal) ->
    case (findProposalMarker transferProposal markers, transferProposal.step.ibans) of
      (None, _) ->
        pure ()
      (Some marker, SenderAndReceiver {sender, receiver}) -> do
        handleProposalForSender transferProposal sender blacklist cid
        handleProposalForReceiver transferProposal cid
      (Some marker, SenderOnly sender) ->
        handleProposalForSender transferProposal sender blacklist cid
      (Some marker, ReceiverOnly _) ->
        handleProposalForReceiver transferProposal cid

-- Helper functions
rejectProposal: Text -> ContractId TransferProposal -> TriggerA () ()
rejectProposal reason proposalCid =
  let rejection = RejectProposal with reason = Some reason in
  void $ emitCommands [exerciseCmd proposalCid rejection] [toAnyContractId proposalCid]

approveProposal: Text -> ContractId TransferProposal -> Optional (ContractId Balance) -> TriggerA () ()
approveProposal reason proposalCid balanceCidOpt =
  let approval = ApproveProposal with
                      reason = Some reason;
                       settleOnLedger = True
      pendingCids = map toAnyContractId (optionalToList balanceCidOpt) <> [toAnyContractId proposalCid] in
  void $ emitCommands [exerciseCmd proposalCid approval] pendingCids

involveBlackListParty : TransferProposal -> Blacklist -> Bool
involveBlackListParty TransferProposal{step, approvers} Blacklist{banks, accounts} =
  let receiverIsBlackListed = optional False (`elem` accounts) (receiverOf step)
      senderIsBlackListed = optional False (`elem` accounts) (senderOf step)
      bankOnSettlementChainBlackListed = any (`elem` banks) approvers in
  receiverIsBlackListed || senderIsBlackListed || bankOnSettlementChainBlackListed

findProposalMarker : TransferProposal -> Map.Map Text AutoApproveTransferProposalMarker -> Optional AutoApproveTransferProposalMarker
findProposalMarker transferProposal@TransferProposal{step} markers =
  case step.ibans of
    SenderOnly iban -> do
      marker <- Map.lookup iban markers
      if matchesMarker transferProposal.step.delivery.amount marker then Some marker else None
    ReceiverOnly iban -> do
      marker <- Map.lookup iban markers
      if matchesMarker transferProposal.step.delivery.amount marker then Some marker else None
    SenderAndReceiver {sender=senderIban, receiver=receiverIban} -> do
      marker1 <- Map.lookup senderIban markers
      marker2 <- Map.lookup receiverIban markers
      pure $ getStrictMarker marker1 marker2

findBalance : TransferProposal -> Text -> TriggerA () (Optional (ContractId Balance, Balance))
findBalance transferProposal@TransferProposal{owner; step} iban = do
  let
    Instrument{amount, label} = step.delivery
    amountRequired = amount
    currencyRequired = label
    identifier = toIdentifier transferProposal
    provider = owner
  queryContractKey (BalanceKey with ..) >>= \case
    Some balance@(_, Balance{amount;currency}) ->
      if currency /= currencyRequired then
        pure $ trace ("Account " <> iban <> "does not hold required currency " <> currencyRequired) None
      else pure $ Some balance
    None -> pure $ trace ("No Balance exist for account" <> iban) None

handleProposalForSender : TransferProposal -> Text -> Optional Blacklist -> ContractId TransferProposal -> TriggerA () ()
handleProposalForSender transferProposal sender blacklist cid = do
  maybeBalance <- findBalance transferProposal sender
  case maybeBalance of
    Some (balanceCid, balance) | transferProposal.step.delivery.amount <= balance.amount ->
        case blacklist of
          Some list | involveBlackListParty transferProposal list ->
              rejectProposal "Rejected due to blacklist" cid
          _ ->
              approveProposal
                "Approved Proposal as sender has enough balance and no banks/accounts are blacklisted"
                cid
                (Some balanceCid)
    _ ->
      rejectProposal "Rejected due to not enough balance"  cid

handleProposalForReceiver : TransferProposal -> ContractId TransferProposal -> TriggerA () ()
handleProposalForReceiver transferProposal cid =
  approveProposal "Approved Proposal for receiver" cid None

matchesMarker : Decimal -> AutoApproveTransferProposalMarker -> Bool
matchesMarker _ AutoApproveTransferProposalMarker { autoApproveType = FullAuto } = True
matchesMarker amount AutoApproveTransferProposalMarker { autoApproveType = LimitedMaxAmount maxAmount } = amount <= maxAmount

getStrictMarker : AutoApproveTransferProposalMarker -> AutoApproveTransferProposalMarker -> AutoApproveTransferProposalMarker
getStrictMarker m1@AutoApproveTransferProposalMarker { autoApproveType = LimitedMaxAmount maxAmount1 }
                m2@AutoApproveTransferProposalMarker { autoApproveType = LimitedMaxAmount maxAmount2 } = if maxAmount1 < maxAmount2 then m1 else m2
getStrictMarker m@AutoApproveTransferProposalMarker { autoApproveType = LimitedMaxAmount maxAmount } _ = m
getStrictMarker _ m@AutoApproveTransferProposalMarker { autoApproveType = LimitedMaxAmount maxAmount } = m
getStrictMarker m _                                                                                    = m
